apply plugin: 'war'
apply plugin: 'liberty'

group = 'org.aguibert'
version = '1.0-SNAPSHOT'
description = "Basic Liberty repo"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.6'
    }
}

repositories {
    mavenCentral()
    maven {
      url 'https://repo.spring.io/milestone'
    }
}

dependencies {
	compileOnly 'org.apache.derby:derby:10.14.1.0'
	compileOnly 'javax:javaee-api:8.0'
	compile group: 'io.r2dbc', name: 'r2dbc-h2', version: '1.0.0.M6'
    libertyRuntime group: 'io.openliberty', name: 'openliberty-runtime', version: '18.0.0.3'
}

ext {
	httpPort = 8080
	httpsPort = 8443
	appUrl = 'http://localhost:8080/basic-liberty-1.0-SNAPSHOT/'
}

liberty {
	server {
		name = rootProject.name + 'Server'
		dropins = [war]
		bootstrapProperties = ['httpPort': httpPort, 'httpsPort': httpsPort]
		configDirectory = file('src/main/liberty/config')
	}
    install {
        //runtimeUrl = "https://public.dhe.ibm.com/ibmdl/export/pub/software/websphere/wasdev/downloads/wlp/beta/wlp-beta-2017.12.0.0.zip"
    }
}

eclipse {
    classpath {
        defaultOutputDir =  file('build/classes/java/main')
        file {
            whenMerged {
                entries.findAll { it.path.startsWith('src/test') }
                       .each { it.output = "build/classes/java/test" }
            }
        }
    }
}

task openBrowser {
    description = "Open browser to ${appUrl}"
    doLast {
        java.awt.Desktop.desktop.browse "${appUrl}".toURI()
    }
}

clean.dependsOn 'libertyStop'
libertyPackage.dependsOn 'libertyStop'
libertyStart.dependsOn 'libertyStop'
libertyRun.dependsOn 'libertyStop'
libertyStart.doLast {
	println "Application available at: ${appUrl}"
}

libertyStop.doLast {
  println "Remove classcache"
  delete "build/wlp/usr/servers/.classCache"
}

libertyStart.dependsOn 'libertyStop'
libertyRun.dependsOn 'libertyStop'

task start { dependsOn 'libertyStart' }
task stop  { dependsOn 'libertyStop' }

